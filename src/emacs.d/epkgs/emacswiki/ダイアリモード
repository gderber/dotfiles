[[en:DiaryMode]]

カレンダー(`M-x calendar')はあなたが`d'を押した時特定の日のためにダイアリのエントリをあなたに示すことができる(カレンダーの詳細については、カレンダーモードを参照されたし).
あなたは同様にイベントが起こる数分前のリマインダーを得るためにアポイントメントモード興味があるかもしれない.

ダイアリのエントリは ~/diary ファイルへ手動で追加される.
'M-x diary' ファイルをは作らない.

== 凝った表示 ==
あなたのカレンダーと日記を凝った表示をするようにするいくつかのコード:

 (setq view-diary-entries-initially t
       mark-diary-entries-in-calendar t
       number-of-diary-entries 7)
 (add-hook 'diary-display-hook 'fancy-diary-display)
 (add-hook 'today-visible-calendar-hook 'calendar-mark-today)

日付の下の醜い等号を取り除くためのいくつかのコード.

 (add-hook 'fancy-diary-display-mode-hook
	   '(lambda ()
              (alt-clean-equal-signs)))
  
 (defun alt-clean-equal-signs ()
   "この関数は=サインの行を見えなくする."
   (goto-char (point-min))
   (let ((state buffer-read-only))
     (when state (setq buffer-read-only nil))
     (while (not (eobp))
       (search-forward-regexp "^=+$" nil 'move)
       (add-text-properties (match-beginning 0) 
	                    (match-end 0) 
			    '(invisible t)))
     (when state (setq buffer-read-only t))))

注意:使われたフックはEmacsの最近のバージョンにのみ存在します; そう, あなたは恐らく次が必要です:

 (define-derived-mode fancy-diary-display-mode  fundamental-mode
   "Diary"
   "凝った表示を使ってダイアリエントリを表示している間使われるメジャーモード."
   (set (make-local-variable 'font-lock-defaults)
        '(fancy-diary-font-lock-keywords t))
   (define-key (current-local-map) "q" 'quit-window)
   (define-key (current-local-map) "h" 'calendar))
 
 (defadvice fancy-diary-display (after set-mode activate)
   "*Fancy Diary Entries* バッファーのモードを `fancy-diary-display-mode' へセットする."
   (save-excursion
     (set-buffer fancy-diary-buffer)
     (fancy-diary-display-mode)))

Then play with the font-lock keywords!
もしあなたのダイアリのバージョンが(現在のXEmacsダイアリパッケージのような)凝った表示のためのフォントロックを持っていないならば, あなたは適切なものを手早く作るためにgeneric.elを使うことができる.

例えば:

    (require 'generic)
    (define-generic-mode 'fancy-diary-display-mode
      nil
      (list "Exception" "Location" "Desc")
      '(
        ("\\(.*\\)\n\\(=+\\)"            ;; Day title / separator lines
         (1 'font-lock-keyword-face) (2 'font-lock-preprocessor-face))
        ("^\\(todo [^:]*:\\)\\(.*\\)$"   ;; To do entries
         (1 'font-lock-string-face) (2 'font-lock-reference-face))
        ("\\(\\[.*\\]\\)"                ;; Category labels
         1 'font-lock-constant-face)
         ("^\\(0?\\([1-9][0-9]?:[0-9][0-9]\\)\\([ap]m\\)?\\(-0?\\([1-9][0-9]?:[0-9][0-9]\\)\\([ap]m\\)?\\)?\\)"
         1 'font-lock-type-face))        ;; Time intervals at start of lines.
      nil
      (list
        (function
          (lambda ()
            (turn-on-font-lock))))
      "Mode for fancy diary display.")


== ローカライゼーション ==

CalendarLocalizationを参照されたし.

== ダイアリエントリ ==

もしあなたが`(european-calendar)'関数を呼ぶならば `european-calendar-style'変数をtにセットし; あなたの日付は日月年の順番になります.
もしそれがnilならば,月日年を使います.

日付と日付のパータンを指定するための多くの異なる方法があります, そのうちのいくつかは'''calendar'''(1)コマンドと互換です.
詳しくはEmacsManualの"Date Formats"をご覧ください.
<b>次の例はヨーロッパのカレンダー様式として仮定します!</b>

ここには特定の日のエントリがあります:

 6 Nov 2000 cinema

複数行のエントリが同じく許されます:

 6 Nov 2000 cinema
               - watch movie
               - party all night

もしあなたがエントリをダイアリエントリとして表示したい(しかしあなたのカレンダーをマークしたくない)のならば,アンパサンド("&")を初めにつけろ:

 &7 Nov 2000 drink water watch TV

アニバーサリーのための例がある.
ダイアリのエントリはおまけに私の親父の年を表示するだろうから注意しろ.

 %%(diary-anniversary 14 2 1940) Dad turns %d

ここに日のブロック全体のための例がある.

 %%(diary-block 14 2 2000 18 2 2000) Software Test

ここに2003年6月15日から始まって60日全ての日の例がある:

 %%(diary-cyclic 60 15 7 2003) Renew medication

ここに週の特定の日がある.どう前についている "&" が私のカレンダーでエントリを目立つことから防いでいるか注意しろ.

 &%%(= 4 (calendar-day-of-week date)) Salsa

そしてここにはいくつかの複雑な表現があります.
これは最初に私達が金曜日(日数にして5)にいるかどうか決め, その後それは日付があるブロック内に収まるかどうかチェックする.
 %%(and (= 5 (calendar-day-of-week date))
        (diary-block 20 10 2000 20 2 2001)) Luzern

ここには毎月第一営業日のために同じくより凝ったエントリがあります:
私達が話している月曜から金曜まで(日数にして1から5まで)どちらかについてそしてそれは月の初めの日,
かもしくはそれは月曜そしてその月の2番目の日,かもしくはそれは月曜そしてその月の3番目の日.

 %%(let ((d (calendar-day-of-week date)))
     (or (and (> d 0)
              (< d 6)
              (diary-date 1 t t))
         (and (= d 1)
              (or (diary-date 2 t t)
                  (diary-date 3 t t))))) Check reports

もしあなたが今後のイベントのリマインダーを見たいならば, <code>diary-remind</code>を使え.
最初の引数は先行している例で使われているひとつようなdiary-sexpです(クウォートを探し出すのに必要とされるしばらくの間は取られます...),
2番目の引数はリマインダーが起こすべきイベント前の日数(もしくは番号のリスト)です.
オプションの第3の引数はカレンダーにリマインダーのマーキングをオンにする.

 %%(diary-remind '(diary-date 1 8 2004) '(1 2 3) t) really-important-event-i-have-to-prepare-days-in-advance

もしあなたがセパレーターを挿入したいならば, 次を試せ:

 %%(progn " ")

もしくは

 %%(progn t) ----

== 新しいダイアリS式 ==

あなたはダイアリS式でたくさんのことをすることができるしかしもしあなたが頻繁にとても複雑なことをするならば、新しいS式を定義することがより簡単になるでしょう...

=== スケジュール ===

もしあなたが学校もしくは大学のためにスケジュールを書きたいならば, あなたは(ダイアリブロックから派生した)2つの日付と平日の間のブロックを定義することを必要とする.
次の関数は同じく祝日を認識するそしてあなたに送りませんこれらの日に学校に... :)

    (defun diary-schedule (m1 d1 y1 m2 d2 y2 dayname)
      "もし日付が日付とDAYNAMEの間ならばエントリは適用される.
      パラメータの順序は`european-calendar-style'がnilならばM1, D1, Y1, M2, D2, Y2,
      `european-calendar-style'がtならばD1, M1, Y1, D2, M2, Y2となります.
      エントリはヒストリでは適用されません."

      (let ((date1 (calendar-absolute-from-gregorian
                    (if european-calendar-style
                        (list d1 m1 y1)
                      (list m1 d1 y1))))
            (date2 (calendar-absolute-from-gregorian
                    (if european-calendar-style
                        (list d2 m2 y2)
                      (list m2 d2 y2))))
            (d (calendar-absolute-from-gregorian date)))
        (if (and 
             (<= date1 d) 
             (<= d date2)
             (= (calendar-day-of-week date) dayname)
             (not (check-calendar-holidays date))
             )
             entry)))

その後: <code>"&%%(diary-schedule 22 4 2003 1 8 2003 2) 18:00 History"</code>


=== カウントダウン ===

これはデッドラインまで日々のカウントダウンを生み出すために(上で定義された) <code>diary-schedule</code> と <code>diary-remind</code> を使います:

 (defun diary-countdown (m1 d1 y1 n)
   "これまでの過去n日間のリマインダー.パラメータの順はもし`european-calendar-style'がnilならば M1, D1, Y1, N, そしてその他場合は D1, M1, Y1, N となります."
   (diary-remind '(diary-date m1 d1 y1) (let (value) (dotimes (number n value) (setq value (cons number value))))))

その後: 2003年4月22日より前の過去15日間リマインダーを <code>"&%%(diary-countdown 22 4 2003 15) Conference deadline"</code> は表示します.

== ダイアリモードキーマップ ==

<pre>
m  ダイアリのエントリをマークする
d  ダイアリのエントリを観る
s  全てのダイアリのエントリを表示する
id ダイアリのエントリを挿入する
iw 毎週のダイアリのエントリを挿入する
im 毎月のダイアリをエントリを挿入する
</pre>

== サードパーティ提供 ==

=== MHC ===

いくつかの興味深い機能を持っているMHCと呼ばれる代わりのカレンダーモードがある.
それはWebサイトを持っている http://www.quickhack.net/mhc/ (注意:日本語の読解力があると恐らく便利).

受信メールメッセージからタスクリスト/カレンダーへエントリを追加するためのフックを持っている(Gnus, Mew, そして Wanderlustのためのフック関数を提供する)
そしてパイロットと他のポピュラーなハンドヘルドデバイスのカレンダーをマージするためのファシリティがそこにはある.
アポイントメントは標準MHフォルダへ格納される, なので,それらはアクセスされおまけに外部ユーティリティによって変更される.
同様に外部LDAPカレンダーサポートがそこにはある.

=== iCalendar インポート/エクスポート ===

UlfJasperはEmacsダイアリのiCalendarイベントを操作するためパッケージを書いた.
news:gnu.emacs.sourcesのアナウンスを読む
　
 EmacsダイアリへシンプルにiCalendarイベントをインポートするためのパッケージがここにある.

 注意してください,これはプリアルファスナップショットトライアルデモテストバージョンです.
 それは通常は正しく働くべきです(例えば 非定期的)イベント
 しかしそれは大抵の定期的イベントで失敗するだろう.
 とにかく, 私が思うに便利なそれを探すことができた誰かがいるだろうから私はそれをここに投稿する.

iCalendar.elの最新版は同じくiCalendarフォーマットでEmacsダイアリファイルのエクスポートを可能にする.
iCalendar.el は(2004年11月10日)現在 GNU Emacsの一部です.

私はiCalendar.elを改良するためにハッキングしている.
私のパッチは http://luca.pca.it/projects/elisp/ で利用可能だ
最新のは次のだ -- LucaCapello (2005年9月15日)

それはすごい.
私は webcal URI を私のダイアリファイルへインクルードすることを可能にしたい.
私が見たいものは複数カレンダーのためのサポートですそうして私はそれらを原子的にもしくは全てもしくはそれらの一部をエクスポートできた...
私に欠けているものはEmacsから一元的にダイアリ/カレンダー/... データへアクセスする方法.
ソートしたものにパームのようなポータブルデバイスでもっと双方向の同期を加える全てのための一元的なインターフェースようなものを持っていることはすごいことだろう

<pre>
+ 20050915-15:30 
      add var icalendar-default-diary-file 
      add var icalendar-use-default-diary-file 
      add var icalendar-kill-buffer-exported 
      mod (icalendar-export-file) 
      mod (icalendar-export-region) 
</pre>

[new]
フルインポート/エクスポートやwebcalサポートのような何か利用可能なアップデートはない? 
:私は機能的なインポート/エクスポートパッチ持っている, 私はchangelogの準備を必要としその後UlfJasperへ送信する, なので好ご期待. そして, ところで webcalサポートは私のToDoリスト上にある... -- LucaCapello (2005-09-20)
Oh 私はインポートされたデータについて問題を持っている. アカウントデータフィールド考慮されず全てのダイアリのページにインポートされたイベントが表示される...

: 私にバグを送ってください --UlfJasper (2005-09-19)
[new:BrandonWMaister:2009-09-28 22:50 UTC]
私はurlを目的のディレクトリへダウンロードするためにconfファイルを使うシンプルで恐らく壊れているスクリプトを作ったそして2005年と2009年の間にいつかメインラインへそれを作っているようなiCalendarインポート機能を使うだろう(あなたが指定したパスへ)"calendars.el" ファイルを自動的に作成する.
[http://svn.quodlibetor.com/wsvn/Hacks/py/ical-in-emacs/#_py_ical-in-emacs_ my svn repo]で利用可能です.
バグ, 暴言, 要望歓迎.

=== CSVファイルからのインポート ===
あなたは''lookout.el''を使ってダイアリデータをCSV(カンマ区切り値)ファイルからインポートできる.
このパッケージはCSVファイルからカレンダーとアドレスデータをインポートすることを可能にする.
(注意:あなたは同じく ''csv.el'' を必要とする.それは同じ場所で見つけることができます.)BbdbImportersを参照されたし.

== 来たるダイアリエントリをメールする ==

シェルスクリプトで何か次のようなコードをひょいと動かす, そしてcronからそれを実行し, あなたの毎朝にメールされる7日間の日程を得る:
    emacs --batch --eval "(setq diary-mail-days 7 european-calendar-style t
                                diary-file \"~/.diary\" diary-mail-addr \"diary@example.com\")"
                         -l diary-lib -f diary-mail-entries

これを実行する前にメールアドレスを変えることを忘れないように.

現在(2003年04月25日)のCVSのコードはバグ(間違った引数の数)のせいで動かない.
このパッチはもう一度それが動くようにする:

    Index: lisp/calendar/diary-lib.el
    ===================================================================
    RCS file: /cvsroot/emacs/emacs/lisp/calendar/diary-lib.el,v
    retrieving revision 1.62
    diff -r1.62 diary-lib.el
    657c657
    <     (funcall (get mail-user-agent 'sendfunc))))
    ---
    >     (funcall (get mail-user-agent 'sendfunc) nil))) 

RMSによるとそれはsendmail.elで修正されるがしかしまだその次にこれはそれをするべきです.
もしあなたが適切に設定されたgnusを持っているならばあなたは同じく"mail-user-agent 'gnus-user-agent" --eval へ追加できる

== シェルに表示されたダイアリエントリを得る ==

calendar のman-pageで説明されているように昔の標準UNIXプログラム '''calendar'''(1) はEmacsのダイアリモードに似た構文を使います(なので簡単にそれをそのシェルログインスクリプトで実行するのは今日と明日のスケジュールがなんであるかを見るためには十分である).
デフォルトのカレンダーでは<code>.calendar</code> もしくは <code>.calendar/calendar</code>ファイルを使う,
なのでひとつはどちらか<code>-f</code> スイッチを使わなければならない, 同じファイルを適切に使うためにファイルシステムリンクもしくは<code>diary-file</code>をセットする.
注意:カレンダーの最近のバージョンがするようにローカライゼーションはちょっと手こずる(ダイアリファイルでLANG=''locale''をEmacsではサポートしない).
もしカレンダーの構文が限定されすぎている, がしかしあなたがまだシェルに印刷される今度のダイアリエントリを得たいなら, [http://netzwurm.de/download/programming/diary-batch.el|http://netzwurm.de/download/programming/diary-batch.el]のオプションを適用させる
[http://netzwurm.de/download/programming/diary-batch.el|http://netzwurm.de/download/programming/diary-batch.el] => 404 Not Found.
これを得るためのその他のURLは?
あなたのニーズのために, あなたの .elisp ディレクトリにファイルを置け  そして呼べ:

  emacs -batch --no-site-file -l .elisp/diary-batch.el -e diary-batch 2>&1 | tail +3

もしあなたが上の行をあなたの .profile もしくは .bash_profile へ置くならば, あなたのログインするときならいつでも今度のダイアリエントリは表示されるだろう.
注意: これはそれらのカーネルコマンドラインでinit=/usr/bin/emacsとともにユーザのために廃止された. ;)
私はdiary-batch.elを探すことができない, なので私は私自身のを作った. それはシンプルで, しかしよくやる. それは上記と同様に呼び出される:

<pre>
(require 'calendar)
(defun diary-batch (&optional ndays)
  (interactive "P")
  (let ((diary-print-entries-hook (lambda () (princ (buffer-string))))
	(diary-display-function 'diary-fancy-display))
    (diary-list-entries (calendar-current-date) (or ndays diary-mail-days))
    (diary-print-entries)))
</pre>

== 質問 ==

私はダイアリ/カレンダー ディスプレイを持ちたい,そして ISO -true- 形式で日付を認識させる ,例えば 2006-03-01, 2006-3-1ではなく. 
どうやってこれを成し遂げるか何かアイディアは?

//Nate, Emacs 21.4 を使用中

* もしあなたがあなたの .emacs ファイルに (calendar-set-date-style 'iso) を置くならば, Emacs 23 はこれをやるために現れる.

通常のダイアリエントリを(いくつかのフェースで)マークできるようにする同じ方法で私はM-x calendar でマークされるために私のダイアリにアニバーサリーエントリが欲しい.
これは可能か?
//Daniel Lublin, Emacs 21.2 を使用中

* アニバーサリーは単に他のエントリのように振る舞うべき. それらは`diary-face' もしくは `diary-entry-marker'をもってカレンダーバッファーでマークされるはずです.(それらがもちろん&で隠されない限り.) -- ulf
* 正しいアニバーサリーの`年'をセットしてみてください, なぜなら0周年アニバーサリー(0周年誕生日)はマークされないからです. --poppyer

異なるS式のために(アニバーサリー, ブロックなど)異なるフェース(マークス/カラー)をセットするための単純な方法があるか?

もしくは私はそれぞれのダイアリアイテムのためにフェース(マーク/カラー)をセットするのを簡単にできるか? -- poppyer

アンパサンドの意味を"反転"させることは可能か (Emacs 21.2)?　それは, (現在のように)それの無効化の代わりにアンパサンドはカレンダーでマーキングを有効にするだろうか? ありがとう! -- ttn
* あなたが欲しいものは正確ではない, しかしもしあなたが "i d" を押したいならば , そしてプレフィックス引数を使うことなく非マーキングエントリを得たいならば, make-diary-entryを単に変えろ
BruceDurlingDotEmacsありがとう, 私は私の持つ質問に答えを見つけた: なぜそれらが表示された時に私のダイアリエントリはソートされる? -- ats
* 答え: 通常は, オプションです. 次を .emacs へ追加しろ, そしてたとえあなたがあなたのダイアリで時間と日付の形式を混在させた時でも, ナイスなソートされたダイアリ表示を楽しめ:
    (add-hook 'list-diary-entries-hook 'sort-diary-entries t)


----
[[PIM]]
[[カテゴリ・カレンダー]]
