Since both JdeeFlymake and the alternative were not working for me I gathered some code together to make a working solution (the process was also documented on my [http://www.credmp.org/2007/07/on-the-fly-syntax-checking-java-in-emacs/ blog].

You will need the Eclipse JDT batch compiler from the eclipse download site.

    (require 'flymake)
    
    (defun flymake-java-ecj-init ()
      (let* ((temp-file   (flymake-init-create-temp-buffer-copy
                           'jde-ecj-create-temp-file))
             (local-file  (file-relative-name
                           temp-file
                           (file-name-directory buffer-file-name))))
        ;;; changes path to ecj.jar here
        (list "java" (list "-jar" "/path/to/ecj.jar" "-Xemacs" "-d" "/dev/null" "-source" "1.5" "-target" "1.5"
                           "-proceedOnError"
                           "-sourcepath" (car jde-sourcepath) "-classpath" (jde-build-classpath jde-global-classpath) local-file))))
    
    (defun flymake-java-ecj-cleanup ()
      "Cleanup after `flymake-java-ecj-init' -- delete temp file and dirs."
      (flymake-safe-delete-file flymake-temp-source-file-name)
      (when flymake-temp-source-file-name
        (flymake-safe-delete-directory (file-name-directory flymake-temp-source-file-name))))
    
    (defun jde-ecj-create-temp-file (file-name prefix)
      "Create the file FILE-NAME in a unique directory in the temp directory.
    This function uses `random' to generate a \"unique\" directory
    name. It doesn't just create the file in the temp directory to
    prevent another emacs process on this same machine from trying to
    use the same file.  PREFIX is ignored in this function as java
    compilers want the temporary file to have the same name as the
    orginal file."
      (file-truename (expand-file-name (file-name-nondirectory file-name)
                                       (expand-file-name  (int-to-string (random)) (flymake-get-temp-dir)))))
    
    (push '(".+\\.java$" flymake-java-ecj-init flymake-java-ecj-cleanup) flymake-allowed-file-name-masks)
    
    (push '("\\(.*?\\):\\([0-9]+\\): error: \\(.*?\\)\n" 1 2 nil 2 3 (6 compilation-error-face)) compilation-error-regexp-alist)
    (push '("\\(.*?\\):\\([0-9]+\\): warning: \\(.*?\\)\n" 1 2 nil 1 3 (6 compilation-warning-face)) compilation-error-regexp-alist)
