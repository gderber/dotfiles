Here's a bit of code to enable on-the-fly syntax checking in ruby. You'll need emacs 22, which includes flymake mode.
<pre>
(require 'flymake)

;; I don't like the default colors :)
(set-face-background 'flymake-errline "red4")
(set-face-background 'flymake-warnline "dark slate blue")

;; Invoke ruby with '-c' to get syntax checking
(defun flymake-ruby-init ()
  (let* ((temp-file   (flymake-init-create-temp-buffer-copy
                       'flymake-create-temp-inplace))
	 (local-file  (file-relative-name
                       temp-file
                       (file-name-directory buffer-file-name))))
    (list "ruby" (list "-c" local-file))))

(push '(".+\\.rb$" flymake-ruby-init) flymake-allowed-file-name-masks)
(push '("Rakefile$" flymake-ruby-init) flymake-allowed-file-name-masks)

(push '("^\\(.*\\):\\([0-9]+\\): \\(.*\\)$" 1 2 nil 3) flymake-err-line-patterns)

(add-hook 'ruby-mode-hook
          '(lambda ()

	     ;; Don't want flymake mode for ruby regions in rhtml files and also on read only files
	     (if (and (not (null buffer-file-name)) (file-writable-p buffer-file-name))
		 (flymake-mode))
	     ))
</pre>

----

A somewhat neater version which keeps temp files in the system temp dir is here:

http://github.com/purcell/emacs.d/blob/master/site-lisp/flymake-ruby/flymake-ruby.el

----

Feature request: have flymake create its temp files in the system temp file directory instead of in the same directory as the file. When using it with Ruby on Rails and autotest, autotest sees the temp file and tries to do something with it and dies, forcing me to restart it, thus killing the magic of autotest. Putting flymake's temp files elsewhere seems like the easiest way to dodge this. 

I second the above request. I know there are workarounds for autotest, but it seems like we don't want to find work arounds for every new web framework, we want to get flymake working in a way that won't conflict with any other tools.


It is easy to patch your autotest to ignore flymake files. I have submitted a patch which hopefully will be included in future releases. For more info see: [http://www.tomtenthij.com/2007/12/10/emacs-flymake-and-autotest-the-fix Emacs, flymake and autotest: the fix]

[new]

As of version 4.1.4 (and probably earlier), Autotest can be configured to ignore Flymake files. In ~/.autotest, add this:

<pre>
Autotest.add_hook :initialize do |autotest|
  autotest.add_exception(/_flymake\.rb$/)
  false
end
</pre>

[new]
Here is a suggestion for a solution (100% untested).
Replace flymake-create-temp-inplace above with

<pre>
(defun flymake-create-temp-intemp (file-name prefix)
  "Return file name in temporary directory for checking FILE-NAME.
This is a replacement for `flymake-create-temp-inplace'. The
difference is that it gives a file name in
`temporary-file-directory' instead of the same directory as
FILE-NAME.

For the use of PREFIX see that function.

Note that not making the temporary file in another directory
\(like here) will not if the file you are checking depends on
relative paths to other files \(for the type of checks flymake
makes)."
  (unless (stringp file-name)
    (error "Invalid file-name"))
  (or prefix
      (setq prefix "flymake"))
  (let* ((name (concat
                (file-name-nondirectory
                 (file-name-sans-extension file-name))
                "_" prefix))
         (ext  (concat "." (file-name-extension file-name)))
         (temp-name (make-temp-file name nil ext))
         )
    (flymake-log 3 "create-temp-intemp: file=%s temp=%s" file-name temp-name)
    temp-name))
</pre>

Can someone please test and see if this works?

Above confirmed to work in Emacs 23.2.91.1 on a Windows box.

----

Here's some code to check ERB templates with flymake. You can find a detailed explanation of what this code does in [http://mnemonikk.org/2010/11/05/using-flymake-to-check-erb-templates/ this post]. 

<pre>
(defun flymake-erb-init ()
  (let* ((check-buffer (current-buffer))
         (temp-file (flymake-create-temp-inplace (buffer-file-name) "flymake"))
         (local-file (file-relative-name
                      temp-file
                      (file-name-directory buffer-file-name))))
    (save-excursion
      (save-restriction
        (widen)
        (with-temp-file temp-file 
          (let ((temp-buffer (current-buffer)))
            (set-buffer check-buffer)
            (call-process-region (point-min) (point-max) "erb" nil temp-buffer nil "-x"))))
      (setq flymake-temp-source-file-name temp-file)
      (list "ruby" (list "-c" local-file)))))

(eval-after-load "flymake"
  '(progn
     (push '(".+\\.\\(rhtml\\|erb\\)$" flymake-erb-init) flymake-allowed-file-name-masks)
     (push '("^\\(.*\\):\\([0-9]+\\): \\(.*\\)$" 1 2 nil 3) flymake-err-line-patterns)))

(defun turn-on-flymake-for-erb-files ()
  (when (string-match "\.erb$" (buffer-file-name))
    (flymake-mode 1)))
(add-hook 'find-file-hook 'turn-on-flymake-for-erb-files)
</pre>

----
A bit of code from stackoverflow[http://stackoverflow.com/questions/8746671#comments-8746930] to show the error messages:

<pre>
(defun my-flymake-show-help () 
	(interactive)
	(when (get-char-property (point) 'flymake-overlay) 
		(let ((help (get-char-property (point) 'help-echo))) 
			(if help (message "%s" help)))))
</pre>
----
FlyMake
